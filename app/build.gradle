import groovy.swing.SwingBuilder

apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        applicationId "com.marcelotomazini.boardgames.chess"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    gradle.taskGraph.whenReady { taskGraph ->
        if (taskGraph.hasTask(':app:assembleRelease')) {
            android.signingConfigs.release.storeFile     file(askFor("store file"))
            android.signingConfigs.release.storePassword askForPassword("store password")
            android.signingConfigs.release.keyAlias      askFor("key alias")
            android.signingConfigs.release.keyPassword   askForPassword("key password")
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:24.0.0'
    compile 'com.marcelotomazini:chess:0.1.6'
    compile 'me.sneer:sneer-android-api:0.1.9@aar'
}

def askForPassword(description) {
    String userInput = "";
    new SwingBuilder().edt {
        dialog(modal: true,
                title: "User input",
                alwaysOnTop: true,
                resizable: false,
                locationRelativeTo: null,
                pack: true,
                show: true
        ) {
            vbox {
                label(text: "Please enter " + description)
                input = passwordField()
                button(defaultButton: true, text: 'OK', actionPerformed: {
                    userInput = input.password;
                    dispose();
                })
            }
        }
    }

    return userInput;
}

def askFor(description) {
    String userInput = "";
    new SwingBuilder().edt {
        dialog(modal: true,
                title: "User input",
                alwaysOnTop: true,
                resizable: false,
                locationRelativeTo: null,
                pack: true,
                show: true
        ) {
            vbox {
                label(text: "Please enter " + description)
                input = textField()
                button(defaultButton: true, text: 'OK', actionPerformed: {
                    userInput = input.text;
                    dispose();
                })
            }
        }
    }

    return userInput;
}
